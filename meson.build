project('tupl', 'cpp',
  default_options : ['cpp_std=c++20','warning_level=3'],
  meson_version : '>=0.64.0',
  version : '0.5'
)

tests = get_option('tests')    # tests are auto disabled for subprojects
arity = get_option('tupl_max_arity')  # default is 16 tupl elements max
no_lupl = get_option('no_lupl')    # lupl type is generated by default
tupl_dev = get_option('tupl_dev')  # disable for no IREPEAT download
namespace_id = get_option('namespace_id') # default is namespace lml

c_array_support_dep = dependency('c_array_support')

IREPEAT_dep = dependency('IREPEAT', required : tupl_dev.allowed())

subdir('tupl_impl')

tupl_dep = declare_dependency(
  dependencies : [c_array_support_dep, IREPEAT_dep, tupl_impl_dep],
  include_directories : include_directories('.')
)

# tests are auto disabled when used as a subproject
if tests.disable_auto_if(meson.is_subproject()).allowed()

if get_option('snitch_cmake')
snitch_cmake = import('cmake').subproject('snitch')
snitch_dep = snitch_cmake.dependency('snitch')
else
snitch_dep = dependency('snitch')
endif

subdir('tests')

endif
